: undefined-xt true abort" Undefined xt execution request in 'defer'-ed word!" ;
: defer
	create ['] undefined-xt , does> @ execute ;

: is
	' state @ false <>
	if ( compiling)
		postpone literal postpone >body postpone !
	else ( interpreting)
		>body !
	then ; immediate

: action-of
	' state @ false <>
	if ( compiling)
		postpone literal postpone >body postpone @
	else ( interpreting)
		>body @
	then ; immediate

: defer! ( xt2 xt1 -- )
	>body ! ;
: defer@ ( xt1 -- xt2)
	>body @ ;
: holds ( c-addr u --)
	swap over + swap
	0 ?do 1- dup c@ hold loop drop
	;


variable +loop-store
:noname base @ hex >r ." +loop: index, limit, step: " 2dup u. u. rot dup >r u. ." (signed: " . . r> . ." )"cr r> base ! ;
: +loop
	literal postpone dup postpone 2r@ , r> +loop-store ! postpone +loop +loop-store @ >r ; immediate

\ true colon-debug !
include runtests.fth

